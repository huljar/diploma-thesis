set terminal epslatex size 1,1 color colortext header \
    "\\newcommand{\\ft}[0]{\\footnotesize}\\newcommand{\\ty}[0]{\\tiny}"

#set ylabel '\ft Y coordinate'
set yrange [-0.5:7.5]

#set xlabel '\ft X coordinate'
set xrange [-0.5:7.5]

#set palette defined (0 '#ffffff', 40 '#f0f000', 80 '#f00000', 100 '#990000')
set palette defined (0 'sea-green', 30 'yellow', 55 'orange', 80 'red', 100 'dark-red')
#set cblabel '\ft Flits forwarded'
set cbrange [20000:120000]

unset colorbox
unset xtics
unset ytics

set size square

set tmargin at screen 0.98
set rmargin at screen 0.98
set bmargin at screen 0.02
set lmargin at screen 0.02

unset key

set cbtics format '\ty \num{%g}' offset -0.5,0

set view map

# draw first three heatmaps without colorbox
set output '../plots/heatmap-iwa-g2c3-dor.tex'
splot '../data/heatmap.dat' index 1 matrix with image

set output '../plots/heatmap-iwa-g2c3-dm.tex'
splot '../data/heatmap.dat' index 4 matrix with image

set output '../plots/heatmap-iwa-g2c3-romm.tex'
splot '../data/heatmap.dat' index 7 matrix with image

# draw last one with colorbox and tics
set terminal epslatex size 1.5,1 color colortext header \
    "\\newcommand{\\ft}[0]{\\footnotesize}\\newcommand{\\ty}[0]{\\tiny}"

set colorbox vertical user origin 0.75,0.025 size 0.06,0.95
set rmargin at screen 0.65

set output '../plots/heatmap-iwa-g2c3-ramm.tex'
splot '../data/heatmap.dat' index 10 matrix with image
